//===== rAthena Script =======================================
//= Euphy's Quest Shop
//===== By: ==================================================
//= Euphy
//===== Current Version: =====================================
//= 1.6c
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= A dynamic quest shop based on Lunar's, with easier config.
//= Includes support for multiple shops & cashpoints.
//= Item Preview script by ToastOfDoom.
//===== Additional Comments: =================================
//= 1.0 Initial script.
//= 1.2 Added category support.
//= 1.3 More options and fixes.
//= 1.4 Added debug settings.
//= 1.5 Replaced categories with shop IDs.
//= 1.6 Added support for purchasing stackables.
//= 1.6a Added support for previewing costumes and robes.
//= 1.6b Added 'disable_items' command.
//= 1.6c Replaced function 'A_An' with "F_InsertArticle".
//============================================================

// Shop NPCs -- supplying no argument displays entire menu.
//	callfunc "qshop"{,<shop ID>{,<shop ID>{,...}}};
//  ADD YOUR NPC HERE
//============================================================
prt_in,38,107,4	script	Winter Costumes#1	894,{ callfunc "qshop",1; }
prt_in,38,104,6	script	Poring Coins Shop#2	960,{ callfunc "qshop",2,3; }
prt_in,39,115,4	script	Quest Shop#3	667,{ callfunc "qshop",4; }	// call the shop 1 and 2 defined below
prt_in,43,97,8	script	Cash Shop#4	667,{ callfunc "qshop",5,6,7,8,9,10,11,12; }	// call the shop 1 and 2 defined below

// etc.. Add your Shop NPCs 'Quest Shop#XXX' here
//============================================================


// Script Core - DO NOT DUPLICATE THIS NPC !!!!!!!!!!!!!
//============================================================
-	script	quest_shop	-1,{
function Add; function Chk; function Slot;
OnInit:
	freeloop(1);

// -----------------------------------------------------------
//  Basic shop settings.
// -----------------------------------------------------------

	set .Announce,0;	// Announce quest completion? (1: yes / 0: no)
	set .ShowSlot,1;	// Show item slots? (2: all equipment / 1: if slots > 0 / 0: never)
	set .ShowID,0;  	// Show item IDs? (1: yes / 0: no)
	set .ShowZeny,1;	// Show Zeny cost, if any? (1: yes / 0: no)
	set .MaxStack,100;	// Max number of quest items purchased at one time.

// -----------------------------------------------------------
//  Points variable -- optional quest requirement.
//	setarray .Points$[0],"<variable name>","<display name>";
// -----------------------------------------------------------

	setarray .Points$[0],
		"#CASHPOINTS", "Cash Points";


//=====================================================================================
// ------------------- ADD YOUR SHOPS NAME AND ITEMS SHOPS STARTING HERE --------------
//=====================================================================================

// -----------------------------------------------------------
//  Shop IDs -- to add shops, copy dummy data at bottom of file.
//	setarray .Shops$[1],"<Shop 1>","<Shop 2>"{,...};
// -----------------------------------------------------------

	setarray .Shops$[1],
		"Winter Costumes",	// Shop Named 1
		"Consumibles",	// Shop Named 2
		"Costumes",		// Shop Named 3
		"Quest Costumes",		// Shop Named 4
		"50 Cash Points",    // 5
		"70 Cash Points",    // 6
		"100 Cash Points",   // 7
		"120 Cash Points",   // 8
		"150 Cash Points",   // 9
		"200 Cash Points",   // 10
		"300 Cash Points",   // 11
		"500 Cash Points";   // 12

// -----------------------------------------------------------
//  Quest items -- do NOT use a reward item more than once!
//	Add(<shop ID>,<reward ID>,<reward amount>,
//	    <Zeny cost>,<point cost>,
//	    <required item ID>,<required item amount>{,...});
// -----------------------------------------------------------


// Shop 1
	Add(1,20499,1,0,0,23550,50); // Item ID 20499, requiere 50 del item 23550
	Add(1,20257,1,0,0,23550,50); // Item ID 20257, requiere 50 del item 23550
	Add(1,20302,1,0,0,23550,50); // Item ID 20302, requiere 50 del item 23550
	Add(1,20298,1,0,0,23550,50); // Item ID 20298, requiere 50 del item 23550
	Add(1,20396,1,0,0,23550,50); // Item ID 20396, requiere 50 del item 23550
	Add(1,31178,1,0,0,23550,50); // Item ID 31178, requiere 50 del item 23550
	Add(1,31249,1,0,0,23550,50); // Item ID 31249, requiere 50 del item 23550
	Add(1,31509,1,0,0,23550,50); // Item ID 31509, requiere 50 del item 23550
	Add(1,31407,1,0,0,23550,50); // Item ID 31407, requiere 50 del item 23550
	Add(1,19586,1,0,0,23550,50); // Item ID 19586, requiere 50 del item 23550





// Shop 2
	Add(2,12216,1,0,0,7539,2); // LV10 Agi Scroll
	Add(2,12466,1,0,0,7539,2); // LV10 Blessing Scroll
	Add(2,12211,1,0,0,7539,20); // Kafra Card
	Add(2,12909,1,0,0,7539,200); // Kafra Card 10pc Box
	Add(2,13697,1,0,0,7539,20); // Blessing Scroll Box (10)
	Add(2,13698,1,0,0,7539,20); // Increase Agility Scroll Box (10)
	Add(2,14233,1,0,0,7539,50); // Dead Branch box (10)
	Add(2,12103,1,0,0,7539,300); // Bloody Branch
	Add(2,7776,1,0,0,7539,70); // Gym Pass
	Add(2,13710,1,0,0,7539,700); // Gym Pass Box
	Add(2,12887,1,0,0,7539,700); // Infinite Fly Wing
	Add(2,13995,1,0,0,7539,5); // Speed Potion Box (10)
	Add(2,12209,1,0,0,7539,3); // Life Insurance
	Add(2,14533,1,0,0,7539,50); // Field Manual 100%
	Add(2,14535,1,0,0,7539,15); // Medium Life Potion
	Add(2,13714,1,0,0,7539,150); // Medium Life Potion 10 Box
	Add(2,14537,1,0,0,7539,15); // Regeneration Potion
	Add(2,14113,1,0,0,7539,150); // Regeneration Potion Box (10)
	Add(2,14601,1,0,0,7539,50); // Tyr's Blessing
	Add(2,12210,1,0,0,7539,700); // Bubble Gum

// Shop 3
	Add(3,19556,1,0,0,7539,500); // C_Kabuki_Mask
	Add(3,19600,1,0,0,7539,500); // Costume Drooping_Kiehl
	Add(3,19608,1,0,0,7539,500); // C_Chick_Hat
	Add(3,19623,1,0,0,7539,500); // C_Bijofnil_Wings
	Add(3,19660,1,0,0,7539,500); // C_Coppola
	Add(3,19701,1,0,0,7539,500); // C_Red_Bonnet
	Add(3,19747,1,0,0,7539,500); // Costume Tha_Despero Mask
	Add(3,19808,1,0,0,7539,500); // C_Blazing_Sun
	Add(3,19827,1,0,0,7539,500); // C_Amistr_Cap
	Add(3,19831,1,0,0,7539,500); // C_Filir_Hat
	Add(3,19834,1,0,0,7539,500); // C_Baseball_Cap
	Add(3,19837,1,0,0,7539,500); // Costume Asara_Fairy_Hat
	Add(3,19842,1,0,0,7539,500); // C_Puppy_Hat
	Add(3,19860,1,0,0,7539,500); // Costume School_Criatura_Hat
	Add(3,19878,1,0,0,7539,500); // C_Drooping_Bunny
	Add(3,19910,1,0,0,7539,500); // C_Halloween_Hat
	Add(3,19919,1,0,0,7539,500); // Costume Quve_Mask
	Add(3,19959,1,0,0,7539,500); // C_Drooping_Argiope
	Add(3,19969,1,0,0,7539,500); // C_Blazing_Soul
	Add(3,19971,1,0,0,7539,500); // C_Dying_Swan
	Add(3,20042,1,0,0,7539,500); // Costume Hermos_Cap
	Add(3,20085,1,0,0,7539,500); // C_Clover_Silkhat
	Add(3,20160,1,0,0,7539,500); // Costume Magnolia_Hat
	Add(3,20175,1,0,0,7539,500); // C_Diabolic_Headphone


// Shop 4
	Add(4,5137,1,0,0,7539,2000,7047,500,661,3,5141,1,4035,1); // Alice Doll
	Add(4,5208,1,0,0,7539,2000,7015,200,1006,2); // Rideword Hat
	Add(4,5211,1,0,0,7539,2000,5045,1,7063,500); // Dress Hat
	Add(4,5214,1,0,0,7539,1200,1022,500,728,250); // MoonLight Flower Hat
	Add(4,5255,1,0,0,7539,1200,2254,1,2255,1,2274,1,7294,10); // Triple Poring Hat
	Add(4,5305,1,0,0,7539,1200,628,20,7189,120); // Pirate Dagger
	Add(4,5307,1,0,0,7539,2000,587,250,576,250); // Carmen Miranda Hat
	Add(4,5340,1,0,0,7539,2000,7047,500,661,300,5141,1,4035,1); // Defolty Doll Hat
	Add(4,5341,1,0,0,7539,2000,7047,500,661,3,5141,1,4058,1); // Glaris Doll Hat
	Add(4,5356,1,0,0,7539,1200,7225,1000,706,20); // Festival Pumpkin Hat
	Add(4,5372,1,0,0,7539,1200,5058,1); // Koneko Hat
	Add(4,5380,1,0,0,7539,700,579,800); // Fish Head Hat
	Add(4,5381,1,0,0,7539,1200,2236,10,2254,1,7294,20); // Santa Poring Hat
	Add(4,5403,1,0,0,7539,700,1054,350,995,50,996,50); // Fish in Mouth
	Add(4,5409,1,0,0,7539,1200,5019,1,7063,300); // Purple Cowboy Hat
	Add(4,5502,1,0,0,7539,1200,7442,10,923,50); // Necromancer's Hood
	Add(4,5514,1,0,0,7539,1200,721,70,5002,1); // Pisces Diadem
	Add(4,5515,1,0,0,7539,1200,2235,1,720,70); // Pisces Crown
	Add(4,5531,1,0,0,7539,700,7447,250,7448,250,7445,250); // Baby Dragon Hat

//Quest shop 5 (50 points)
	Add(5,31562,1,0,50); // Zealotus Doll
	Add(5,20449,1,0,50); // Black and White Temptation
	Add(5,19934,1,0,50); // Alive Ten Gallon Hat Of Flame
	Add(5,20313,1,0,50); // Costume CD in Mouth
	Add(5,19530,1,0,50); // Wild Rose Hat
	Add(5,20177,1,0,50); // Drooping Dorasuke
	Add(5,18740,1,0,50); // White Phoenix Hair
	Add(5,19917,1,0,50); // Gloomy Pumpkin Hat
	Add(5,19598,1,0,50); // Wondering Wolf Helm
	Add(5,20183,1,0,50); // Floating Ghost
	Add(5,19544,1,0,50); // Drooping Cat Crew
	Add(5,19537,1,0,50); // Gryphon Hat
	Add(5,19667,1,0,50); // Helm of Dragoon
	Add(5,20227,1,0,50); // Husky Hat
	Add(5,19599,1,0,50); // Imp Hat
	Add(5,19712,1,0,50); // Little Angel Doll
	Add(5,19543,1,0,50); // Oliver Wolf Hood
	Add(5,19707,1,0,50); // Pecopeco Cap Costume
	Add(5,20273,1,0,50); // Soft Sheep Hat
	Add(5,19884,1,0,50); // Vanargand Helm
	Add(5,19818,1,0,50); // Droop Morocc Minion
	Add(5,19883,1,0,50); // Piamette Hood
	Add(5,19816,1,0,50); // Pecopeco Cap Costume
	Add(5,20194,1,0,50); // Lion Mask
	Add(5,20268,1,0,50); // Sleep Eclipse Family
	Add(5,20006,1,0,50); // Dark Knight Mask
	Add(5,20045,1,0,50); // Samambaia
	Add(5,20197,1,0,50); // Amistr Beret

// Quest Shop 6 (70 puntos)
	Add(6,19777,1,0,70); // Twinkling Star
	Add(6,20249,1,0,70); // Red Strong Hair
	Add(6,31624,1,0,70); // Moew Lover
	Add(6,20458,1,0,70); // Wild Poring Rider
	Add(6,31179,1,0,70); // Wolf Masquerade
// Quest shop 7 (100 puntos)
	Add(7,31062,1,0,100); // Costume: Eleanor Wig
	Add(7,31405,1,0,100); // Costume: Golden Eleanor Wig
	Add(7,20405,1,0,100); // Costume Eremes' Scarf
	Add(7,31057,1,0,100); // Costume Eremes Scarf (Black)
	Add(7,20481,1,0,100); // Burning Mask of Champions
	Add(7,19294,1,0,100); // Cyber Cat Ear Headphones (Red)
	Add(7,20329,1,0,100); // Very Cute Doll Hat
	Add(7,20036,1,0,100); // Crown of the Sword Master
	Add(7,19823,1,0,100); // Kitsune Mask Hood
	Add(7,19743,1,0,100); // Anubis Helm
// Quest shop 8 (120 puntos)
	Add(8,31186,1,0,120); // Black Cat
	Add(8,31452,1,0,120); // White Cat
	Add(8,31299,1,0,120); // White Rabbit
// Quest Shop 9 (150 puntos)
	Add(9,20247,1,0,150); // Costume Black Hand of Destiny
	Add(9,31437,1,0,150); // Costume Baby Penguin
	Add(9,20349,1,0,150); // Costume Flying Galapago
	Add(9,20404,1,0,150); // Blessing Of Angels
// Quest shop 10 (200 puntos)
	Add(10,20285,1,0,200); // Costume Fluttering Cherry Blossom
	Add(10,31498,1,0,200); // Costume: Elephangel
	Add(10,31395,1,0,200); // Book of Magic
	Add(10,20154,1,0,200); // Falling Leaves
//Quest shop 11 (300 puntos)
	Add(11,18744,1,0,300); // Twilight
//Quest shop 12 (500 puntos)
	Add(12,20761,1,0,500); // Happiness Wings
	Add(12,20765,1,0,500); // Archangel Wing
	Add(12,20764,1,0,500); // Fallen Angel Wing
	Add(12,20727,1,0,500); // Brilliant Golden Wings


// -----------------------------------------------------------

//=====================================================================================
// ------------------- YOUR SHOPS AND ITEMS SHOPS HAVE BEEN ADDED ---------------------
//=====================================================================================

	freeloop(0);
	set .menu$,"";
	for(set .@i,1; .@i<=getarraysize(.Shops$); set .@i,.@i+1) {
		set .menu$, .menu$+.Shops$[.@i]+":";
		npcshopdelitem "qshop"+.@i,909;
	}
	end;

OnMenu:
	set .@size, getarraysize(@i);
	if (!.@size) set @shop_index, select(.menu$);
	else if (.@size == 1) set @shop_index, @i[0];
	else {
		for(set .@j,0; .@j<.@size; set .@j,.@j+1)
			set .@menu$, .@menu$+.Shops$[@i[.@j]]+":";
		set @shop_index, @i[select(.@menu$)-1];
	}
	deletearray @i[0],getarraysize(@i);
	if (.Shops$[@shop_index] == "") {
		message strcharinfo(0),"An error has occurred.";
		end;
	}
	dispbottom "Select one item at a time.";
	callshop "qshop"+@shop_index,1;
	npcshopattach "qshop"+@shop_index;
	end;

OnBuyItem:
	// .@q[] : RewardID, BoughtAmt, RewardAmt, BaseAmt, ReqZeny, ReqPts, { ReqItem, ReqAmt, ... }
	setarray .@q[0],@bought_nameid[0],((@bought_quantity[0] > .MaxStack)?.MaxStack:@bought_quantity[0]);
	copyarray .@q[3],getd(".q_"+@shop_index+"_"+.@q[0]+"[0]"),getarraysize(getd(".q_"+@shop_index+"_"+.@q[0]));
	set .@q[2],.@q[1]*.@q[3];
	if (!.@q[2] || .@q[2] > 30000) {
		message strcharinfo(0),"You can't purchase that many "+getitemname(.@q[0])+".";
		end;
	}
	mes "[Quest Shop]";
	mes "Reward: ^0055FF"+((.@q[2] > 1)?.@q[2]+"x ":"")+Slot(.@q[0])+"^000000";
	mes "Requirements:";
	disable_items;
	if (.@q[4]) mes " > "+Chk(Zeny,.@q[4]*.@q[1])+(.@q[4]*.@q[1])+" Zeny^000000";
	if (.@q[5]) mes " > "+Chk(getd(.Points$[0]),.@q[5]*.@q[1])+(.@q[5]*.@q[1])+" "+.Points$[1]+" ("+getd(.Points$[0])+"/"+(.@q[5]*.@q[1])+")^000000";
	if (.@q[6]) for(set .@i,6; .@i<getarraysize(.@q); set .@i,.@i+2)
		mes " > "+Chk(countitem(.@q[.@i]),.@q[.@i+1]*.@q[1])+((.ShowID)?"{"+.@q[.@i]+"} ":"")+Slot(.@q[.@i])+" ("+countitem(.@q[.@i])+"/"+(.@q[.@i+1]*.@q[1])+")^000000";
	next;
	setarray @qe[1], getiteminfo(.@q[0], ITEMINFO_LOCATIONS), getiteminfo(.@q[0], ITEMINFO_VIEW);
	if (@qe[2] > 0 && ((@qe[1] & EQP_HEAD_LOW) || (@qe[1] & EQP_HEAD_TOP) || (@qe[1] & EQP_HEAD_MID) || (@qe[1] & EQP_COSTUME_HEAD_TOP) || (@qe[1] & EQP_COSTUME_HEAD_MID) || (@qe[1] & EQP_COSTUME_HEAD_LOW) || (@qe[1] & EQP_GARMENT) || (@qe[1] & EQP_COSTUME_GARMENT)))
		set .@preview,1;
	addtimer 1000, strnpcinfo(0)+"::OnEnd";
	while(1) {
		switch(select(" ~ Purchase ^0055FF"+getitemname(.@q[0])+"^000000:"+((.@preview && !@qe[7])?" ~ Preview...":"")+": ~ ^777777Cancel^000000")) {
		case 1:
			if (@qe[0]) {
				mes "[Quest Shop]";
				mes "You're missing one or more quest requirements.";
				close;
			}
			if (!checkweight(.@q[0],.@q[2])) {
				mes "[Quest Shop]";
				mes "^FF0000You need "+(((.@q[2]*getiteminfo(.@q[0], ITEMINFO_WEIGHT))+Weight-MaxWeight)/10)+" additional weight capacity to complete this trade.^000000";
				close;
			}
			if (.@q[4]) set Zeny, Zeny-(.@q[4]*.@q[1]);
			if (.@q[5]) setd .Points$[0], getd(.Points$[0])-(.@q[5]*.@q[1]);
			if (.@q[6]) for(set .@i,6; .@i<getarraysize(.@q); set .@i,.@i+2)
				delitem .@q[.@i],.@q[.@i+1]*.@q[1];
			getitem .@q[0],.@q[2];
			if (.Announce) announce strcharinfo(0)+" has created "+((.@q[2] > 1)?.@q[2]+"x "+getitemname(.@q[0]):callfunc("F_InsertArticle",getitemname(.@q[0])))+"!",0;
			specialeffect2 EF_FLOWERLEAF;
			close;
		case 2:
			setarray @qe[3], getlook(LOOK_HEAD_BOTTOM), getlook(LOOK_HEAD_TOP), getlook(LOOK_HEAD_MID), getlook(LOOK_ROBE), 1;
			if ((@qe[1] & 1) || (@qe[1] & 4096)) changelook LOOK_HEAD_BOTTOM, @qe[2];
			else if ((@qe[1] & 256) || (@qe[1] & 1024)) changelook LOOK_HEAD_TOP, @qe[2];
			else if ((@qe[1] & 512) || (@qe[1] & 2048)) changelook LOOK_HEAD_MID, @qe[2];
			else if ((@qe[1] & 4) || (@qe[1] & 8192)) changelook LOOK_ROBE, @qe[2];
			break;
		case 3:
			close;
		}
	}

OnEnd:
	if (@qe[7]) {
		changelook LOOK_HEAD_BOTTOM, @qe[3];
		changelook LOOK_HEAD_TOP, @qe[4];
		changelook LOOK_HEAD_MID, @qe[5];
		changelook LOOK_ROBE, @qe[6];
	}
	deletearray @qe[0],8;
	end;

function Add {
	if (getitemname(getarg(1)) == "null") {
		debugmes "Quest reward #"+getarg(1)+" invalid (skipped).";
		return;
	}
	setarray .@j[0],getarg(2),getarg(3),getarg(4);
	for(set .@i,5; .@i<getargcount(); set .@i,.@i+2) {
		if (getitemname(getarg(.@i)) == "null") {
			debugmes "Quest requirement #"+getarg(.@i)+" invalid (skipped).";
			return;
		} else
			setarray .@j[.@i-2],getarg(.@i),getarg(.@i+1);
	}
	copyarray getd(".q_"+getarg(0)+"_"+getarg(1)+"[0]"),.@j[0],getarraysize(.@j);
	npcshopadditem "qshop"+getarg(0),getarg(1),((.ShowZeny)?getarg(3):0);
	return;
}

function Chk {
	if (getarg(0) < getarg(1)) {
		set @qe[0],1;
		return "^FF0000";
	} else
		return "^00FF00";
}

function Slot {
	set .@s$,getitemname(getarg(0));
	switch(.ShowSlot) {
		case 1: if (!getitemslots(getarg(0))) return .@s$;
		case 2: if (getiteminfo(getarg(0), ITEMINFO_TYPE) == 4 || getiteminfo(getarg(0), ITEMINFO_TYPE) == 5) return .@s$+" ["+getitemslots(getarg(0))+"]";
		default: return .@s$;
	}
}
}

function	script	qshop	{
	deletearray @i[0],getarraysize(@i);
	for(set .@i,0; .@i<getargcount(); set .@i,.@i+1)
		set @i[.@i],getarg(.@i);
	doevent "quest_shop::OnMenu";
	end;
}


// Dummy shop data -- copy as needed.
//============================================================
-	shop	qshop1	-1,909:-1
-	shop	qshop2	-1,909:-1
-	shop	qshop3	-1,909:-1
-	shop	qshop4	-1,909:-1
-	shop	qshop5	-1,909:-1
-	shop	qshop6	-1,909:-1
-	shop	qshop7	-1,909:-1
-	shop	qshop8	-1,909:-1
-	shop	qshop9	-1,909:-1
-	shop	qshop10	-1,909:-1
-	shop	qshop11	-1,909:-1
-	shop	qshop12	-1,909:-1